buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

version = "${mc_version}-${mod_version}"
group = "com.ewyboy.bibliotheca"
archivesBaseName = "Bibliotheca"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
	version = "${mc_version}-${forge_version}"
	runDir = "run"
	mappings = "${mcp_mappings}"
	makeObfSourceJar = false
}

repositories {  
    maven { url "http://tehnut.info/maven" }
}

dependencies {
    compile "mcp.mobius.waila:Hwyla:${waila_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    classifier = 'universal'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives srcJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty("local_maven")) {
                repository(url: "file://${local_maven}")
                pom {
                    groupId = project.group
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging 'jar'
                        description 'Bibliotheca'
                        url 'https://github.com/EwyBoy/Bibliotheca'

                        scm {
                            url 'https://github.com/EwyBoy/Bibliotheca'
                            connection 'scm:git:git://github.com/EwyBoy/Bibliotheca.git'
                            developerConnection 'scm:git:git@github.com:EwyBoy/Bibliotheca.git'
                        }

                        issueManagement {
                            system 'github'
                            url 'https://github.com/EwyBoy/Bibliotheca/issues'
                        }

                        developers {
                            developer {
                                id 'EwyBoy'
                                name 'EwyBoy'
                                roles { role 'developer' }
                            }
                        }
                    }
                }
            }
        }
    }
}